import static java.lang.System.out

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    ext {
        lombokVersion = '1.18.4'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE"
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'com.palantir.git-version' version '0.12.2'
}

apply plugin: 'maven-publish'

def details = versionDetails()
group 'org.example'
version("1.2-${details.gitHash}-${System.currentTimeMillis()}")

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task BuildAndPrintVersion(dependsOn: ":build") {
    doLast {
        out.println(version)
    }
}

dependencies {
    // JAX-B dependencies for JDK 9+
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"
    
    // spring
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation "org.springframework.boot:spring-boot-starter-jetty"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation 'org.springframework.kafka:spring-kafka'

    // db
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.10'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'

    // monitoring
    implementation group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.3.5'
    implementation group: 'io.micrometer', name: 'micrometer-core', version: '1.3.5'

    // spock
    testImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
    testImplementation 'org.spockframework:spock-spring:1.1-groovy-2.4'

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // others
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
